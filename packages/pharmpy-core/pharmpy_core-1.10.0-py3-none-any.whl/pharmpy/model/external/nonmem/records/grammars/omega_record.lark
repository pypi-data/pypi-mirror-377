// $OMEGA  [DIAGONAL(n)|BLOCK(n)|BLOCK(n) SAME(m)|BLOCK SAME(m)]
//         [BLOCK(n)VALUES(diag,odiag)]
//         [VARIANCE|STANDARD]  [COVARIANCE|CORRELATON] [CHOLESKY]
//         [[value1]  [value2]  [value3] ... [FIXED]]
//         [(value)xn]
//
// 4 legal forms
//   1. $OMEGA   [DIAGONAL(n)] [ v11 v22 v33 ... vnn ]
//   2. $OMEGA BLOCK(n) [ v11 v21 v22 v31 v32 v33 ... vn1 vn2 ... vnn ]
//   3. $OMEGA BLOCK(n) SAME(m)
//   4. $OMEGA BLOCK(n) VALUES(diag,odiag)

// Samples from nmhelp:
//   %input | BLOCK(3)  6. .005 .3 .0002 .006 .4|
//   %input | .04 .12|
//   %input | BLOCK(2) .04 .002 .12|
//   %input | BLOCK(2) SAME(3)|
//   %input | BLOCK(6) VALUES(0.1,0.01)|
//
// All these inputs have the same meaning:
//   %input | BLOCK(2) ; or $OMEGA VARIANCE COVARIANCE BLOCK(2)\n0.64\n-0.24 0.58|
//   %input | STANDARD BLOCK(2)\n0.8\n-0.24 0.762|
//   %input | STANDARD CORRELATION BLOCK(2)\n0.8\n-0.394 0.762|
//   %input | VARIANCE CORRELATION BLOCK(2)\n0.64\n-0.394 0.58|
//   %input | CHOLESKY BLOCK(2)\n0.8\n-0.3 0.7|

root : _record

_record : (diagonal _sep?)? _diag_items                        // form 1
        | _options block _sep? _options _omegas                // form 2
        | _options block _sep? _options same                   // form 3
        | _options bare_block _sep? _options same              // form 3 w/o n
        | _options block _sep? _options values _roptions       // form 4

_options: (_option _sep?)*
_roptions: _roptions? _sep? _option

_diag_items: (_diag_items _sep?)? diag_item
diag_item: init _diag_options?
         | _lpar (_diag_option _sep?)* init _diag_options? _rpar n?
_diag_options: _diag_options? _sep? _diag_option
_diag_option: FIX | SD | VAR

// repeat of init or comment (form 1 & 2)
_omegas : _omegas? omega

// omega init value
omega : init _roptions?
      | _lpar init (_sep? _option)* _rpar n?
      | _lpar (_option _sep?)+ init _rpar n?

// can be tied to WHOLE record or ONE INIT
_option  : FIX | VAR | SD | CORR | COV | CHOLESKY
FIX      : "FIXED" | "FIXE" | "FIX"
VAR      : "VARIANCE" | "VARIANC" | "VARIAN" | "VARIA" | "VARI" | "VAR" | "VA" | "V"
SD       : "STANDARD" | "STANDAR" | "STANDA" | "STAND" | "STAN" | "STA" | "ST" | "SD" | "S"
CORR     : "CORRELATION" | "CORRELATIO" | "CORRELATI" | "CORRELAT" | "CORRELA" | "CORREL" | "CORRE" | "CORR" | "COR"
COV      : "COVARIANCE" | "COVARIANC" | "COVARIAN" | "COVARIA" | "COVARI" | "COVAR" | "COVA" | "COV"
CHOLESKY : "CHOLESKY" | "CHOLESK" | "CHOLES" | "CHOLE" | "CHOL" | "CHO"

// parentheses sizes (optional for SAME and BLOCK with SAME)
BLOCK       : "BLOCK" | "BLOC" | "BLO"
block       : BLOCK _lpar_int size _rpar
bare_block  : BLOCK

SAME.1      : "SAME"
same        : SAME (_lpar_int size _rpar)?

DIAGONAL    : "DIAGONAL" | "DIAGONA" | "DIAGON" | "DIAGO" | "DIAG" | "DIA"
diagonal    : DIAGONAL _lpar_int size _rpar

VALUES      : "VALUES"
values      : VALUES _lpar diag _sep? odiag _rpar

size  : INT
diag  : NUMERIC
odiag : NUMERIC

// separator and clustering of init values
_sep : ","
_lpar : "("
_lpar_int : "("
_rpar : ")"

init  : NUMERIC
n     : X INT
X     : "x"


%import .definitions (INT, NUMERIC, COMMENT, NEWLINE, WS)
%ignore WS
%ignore COMMENT
%ignore NEWLINE

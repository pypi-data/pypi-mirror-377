// from NONMEM 7.4 spec:
//
// $SIMULATION  (seed1 [seed2] [NORMAL|UNIFORM|NONPARAMETRIC] [NEW]) ...
//              [SUBPROBLEMS=n] [ONLYSIMULATION] [OMITTED]
//              [REQUESTFIRST] [REQUESTSECOND] [PREDICTION|NOPREDICTION]
//              [TRUE=INITIAL|FINAL|PRIOR]
//              [BOOTSTRAP=n [REPLACE|NOREPLACE] [STRAT=label] [STRATF=label]]
//              [NOREWIND|REWIND] [SUPRESET|NOSUPRESET]
//              [RANMETHOD=[n|S|m|P] ]
//              [PARAFILE=[filename|ON|OFF]

root : option*

// option rules
?option: "(" INT ")"                        -> seed
       | SUBPROBLEMS "="? INT               -> nsubs
       | ONLYSIMULATION                     -> onlysim
       | OMITTED                            -> omitted
       | PREDICTION                         -> prediction
       | NOPREDICTION                       -> noprediction

// option terminals
SUBPROBLEMS: "SUBPROBLEMS"
           | "SUBPROBS"
           | "SUBPROB"
           | "NSUBPROBLEMS"
           | "NSUBPROBS"
           | "NSUB"

ONLYSIMULATION: "ONLYSIMULATION"
              | "ONLYSIMULATIO"
              | "ONLYSIMULATI"
              | "ONLYSIMULAT"
              | "ONLYSIMULA"
              | "ONLYSIM"

OMITTED: "OMITTED"
PREDICTION: "PREDICTION"
NOPREDICTION: "NOPREDICTION"

%import .definitions (INT, SIGNED_INT, SYMBOL, COMMENT, NEWLINE, WS)
%ignore WS
%ignore COMMENT
%ignore NEWLINE
